---
title: Getting Vertex Light Colors and Direction in Unity Shaders
tags: [Unity, Shaders]
type: guide
created: 2021-12-28T01:42:00+04:00
---

Vertex Colors are this arcane system that isnâ€™t documented well, here are some helper functions

```hlsl
float4x4 getVertexLightsDir(float3 worldPos) {
    float3 toLightX = float3(unity_4LightPosX0.x, unity_4LightPosY0.x, unity_4LightPosZ0.x);
    float3 toLightY = float3(unity_4LightPosX0.y, unity_4LightPosY0.y, unity_4LightPosZ0.y);
    float3 toLightZ = float3(unity_4LightPosX0.z, unity_4LightPosY0.z, unity_4LightPosZ0.z);
    float3 toLightW = float3(unity_4LightPosX0.w, unity_4LightPosY0.w, unity_4LightPosZ0.w);

    float3 dirX = normalize(toLightX - worldPos);
    float3 dirY = length(toLightY) > 0 ? normalize(toLightY - worldPos) : 0;
    float3 dirZ = length(toLightZ) > 0 ? normalize(toLightZ - worldPos) : 0;
    float3 dirW = length(toLightW) > 0 ? normalize(toLightW - worldPos) : 0;

    float4x4 retval = 0;
        retval[0] = float4(dirX, 0);
        retval[1] = float4(dirY, 0);
        retval[2] = float4(dirZ, 0);
        retval[3] = float4(dirW, 0);
    return retval;
}
```

```hlsl
float4x4 getVertexLightsColors(float3 worldPos, float3 normal, bool usendl=true)
{
    float4 toLightX = unity_4LightPosX0 - worldPos.x;
    float4 toLightY = unity_4LightPosY0 - worldPos.y;
    float4 toLightZ = unity_4LightPosZ0 - worldPos.z;

    float4 lengthSq = 0;
    lengthSq += toLightX * toLightX;
    lengthSq += toLightY * toLightY;
    lengthSq += toLightZ * toLightZ;
    lengthSq = max(lengthSq, 0.000001);

    float4 ndl = 0;
    UNITY_BRANCH
    if (usendl) {
        ndl += toLightX * normal.x;
        ndl += toLightY * normal.y;
        ndl += toLightZ * normal.z;
        ndl = max (float4(0,0,0,0), ndl * rsqrt(lengthSq));
        ndl = ndl*0.5+0.5;
    }

    float4 atten = 1.0 / (1.0 + lengthSq * unity_4LightAtten0);
    float4 atten2 = saturate(1 - (unity_4LightAtten0 * lengthSq / 25)); // Black magic
    atten = min(atten, atten2 * atten2); // more black magic
    atten *= usendl ? ndl : 1;
    
    float4x4 retval = 0;
        retval[0] = unity_LightColor[0] * atten.x;
        retval[1] = unity_LightColor[1] * atten.y;
        retval[2] = unity_LightColor[2] * atten.z;
        retval[3] = unity_LightColor[3] * atten.w;
    return retval;
}
```

Yuo can also infer range directly from the attenuation by using this formula

```hlsl
float range = 5.0 * (1.0 / sqrt(unity_4LightAtten0.x));
```